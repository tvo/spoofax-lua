/*
 * Spoofax-Lua: a Lua Eclipse plugin based on Spoofax
 * Copyright (C) 2011  Tobi Vollebregt
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */
module rename-loop

imports
  include/Lua

strategies

  rename-loop-top = {|CurrentLoop: alltd(rename-loop) |}

rules

  // Loop declaration
  declare-loop: x -> x'{y}
    with
      {|CurrentLoop:
        y := <new>;
        rules(
          // LoopDef : y -> x
          CurrentLoop := y
        );
        x' := <all(alltd(rename-loop))> x
      |}

  rename-loop: ForLoop(_, _, _, _, _) -> <declare-loop>
  rename-loop: ForEachLoop(_, _, _) -> <declare-loop>
  rename-loop: RepeatLoop(_, _) -> <declare-loop>
  rename-loop: WhileLoop(_, _) -> <declare-loop>

  // Loop undeclaration
  undeclare-loop: x -> x'
    with
      {|CurrentLoop:
        rules( CurrentLoop :- _ );
        x' := <all(alltd(rename-loop))> x
      |}

  rename-loop: Function(_, _) -> <undeclare-loop>

  // Loop use
  rename-loop: Break() -> <id>{<CurrentLoop>}
