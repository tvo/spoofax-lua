/*
 * Spoofax-Lua: a Lua Eclipse plugin based on Spoofax
 * Copyright (C) 2011  Tobi Vollebregt
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */
module locals

imports
  include/Lua
  lib/editor-common.generated

strategies

  // Based on code snippets in `The Spoofax Language Workbench (DRAFT)'
  // by Lennart C. L. Kats and Eelco Visser.

  rename-top = rename-scope(rename-top')

  rename-top' = alltd(rename)

  rename-scope(s) = {|RenameId: s|}
  rename-scope = rename-scope(all(rename-top'))

rules

  // Variable declaration
  rename: LocalVariableDecl(name*, exp*) -> LocalVariableDecl(name'*, exp'*)
    where
      // NOTE: In `local x; local x = x', the third x refers to the first x!
      //       Therefore, process exp* first and only then name*.
      (exp'*, name'*) := <rename-top'> (exp*, name*)

  rename: d@Name(x) -> Name(y)
    where
      y := x{<new>};
      rules(
        VarDef   : y -> d
        RenameId : x -> y
      )

  // Variable use
  rename: Var(x) -> Var(y)
    where
      y := <RenameId> x;
      rules( VarUse : y -> y )

  // Scopes
  rename: Chunk(_) -> <rename-scope>
  rename: ForLoop(_, _, _, _, _) -> <rename-scope>
  rename: ForEachLoop(_, _, _) -> <rename-scope>

  // Give each function a unique name,
  // to be able to distinguish them during type analysis.
  rename: Function(args, body) -> <rename-scope> Function(args', body'){<new>}
    where
      args' := <rename-top'> args;
      body' := <rename-top'> body

  // Special handling for `repeat ... until exp' loops because
  // the scope of the body includes the loop condition exp.
  rename: RepeatLoop(Chunk(x), exp) -> RepeatLoop(Chunk(y), exp')
    where
      {|RenameId:
        y    := <rename-top'> x;
        exp' := <rename-top'> exp
      |}

  // Completion
  rename: COMPLETION(_) -> <id>
    where
      // NOTE: renamed names are required to be able to get
      //       the type of the names after type analysis.
      all-keys-RenameId; map(RenameId) => proposals;
      rules( ContentProposals := proposals )

  // Give each table constructor a unique name,
  // to be able to distinguish them during type analysis.
  rename: TableCons(x) -> TableCons(y){<new>}
    where
      y := <rename-top'> x
