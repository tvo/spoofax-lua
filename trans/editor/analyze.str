/*
 * Spoofax-Lua: a Lua Eclipse plugin based on Spoofax
 * Copyright (C) 2011  Tobi Vollebregt
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */
module analyze

imports
  lib/editor-common.generated
  include/Lua
  analysis/analyze
  analysis/globals
  analysis/locals

rules

  // Analyzes the current program, returning a tuple with errors, warnings, and notes;
  // each a list of (term, message) tuples or simply (message) terms.
  editor-analyze:
    (ast, path, project-path) -> result
    with
      editor-init;
      ast' := <analyze> ast;
      if !ast' => File(_, _) then
        errors   := <collect-all(check-error, conc)> ast';
        warnings := <collect-all(check-warning, conc)> ast';
        notes    := <collect-all(check-note, conc)> ast';
        result   := (ast', errors, warnings, notes)
      else
        // For syntax tests
        result   := (ast', [], [], [])
      end

rules

  check-note = fail
  check-warning = fail
  check-error = fail

  check-error:
    File(Some(comment), _) -> (comment, $[Construct may not be preceded by layout])
    where
      not(<origin-location> comment => (1,0,_,_))

  check-error:
    Break(){} -> (<id>, "No loop to break")

  // check-note:
  //   Var(x{}) -> (<id>, $[Use of global [x]])
  //   where
  //     <GlobalVarDef> x

  check-warning:
    Var(x{}) -> (<id>, $[Use of unassigned global [x]])
    where
      not(<GlobalVarDef> x) // ever assigned?

  check-warning:
    d@ Var(x{}) -> (<id>, $[Unused global [x]])
    where
      <GlobalVarDef> x; ?d; // first assignment?
      not(<GlobalVarUse> x)    // ever used?

  check-warning:
    Name(x) -> (<id>, $[Unused local [x]])
    where
      not(!x => "self" + !x => "_");
      not(<VarUse> x)
