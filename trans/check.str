module check

imports
  libstratego-lib
  include/Lua
  lib/editor-common.generated
  desugar
  globals
  rename
  rename-loop

rules

  analyze:
    (ast, path, project-path) -> ast'
    with
      ast' := <desugar-top;
               rename-top;
               rename-loop-top;
               globals-top> ast

rules

  check-error:
    file@ File(Some(comment), _) -> (comment, $[Construct may not be preceded by layout])
    where
      <origin-text; not(string-starts-with(|"#"))> file

  check-error:
    Break(){} -> (<id>, "No loop to break")

  check-note:
    Var(x{}) -> (<id>, $[Use of global [x]])
    where
      <GlobalVar> x

  check-warning:
    Var(x{}) -> (<id>, $[Use of unassigned global [x]])
    where
      not(<GlobalVar> x)
