The Complete Syntax of Lua
==========================

Here is the complete syntax of Lua in extended BNF. (It does not describe operator precedences.)

x   chunk ::= {stat [`;´]} [laststat [`;´]]

x   block ::= chunk

x   stat ::=  varlist `=´ explist |
         functioncall |
         do block end |
         while exp do block end |
         repeat block until exp |
         if exp then block {elseif exp then block} [else block] end |
         for Name `=´ exp `,´ exp [`,´ exp] do block end |
         for namelist in explist do block end |
         function funcname funcbody |
         local function Name funcbody |
         local namelist [`=´ explist]

x   laststat ::= return [explist] | break

    funcname ::= Name {`.´ Name} [`:´ Name]

    varlist ::= var {`,´ var}

x   var ::=  Name | prefixexp `[´ exp `]´ | prefixexp `.´ Name

    namelist ::= Name {`,´ Name}

x   explist ::= {exp `,´} exp

x   exp ::=  nil | false | true | Number | String | `...´ | function |
         prefixexp | tableconstructor | exp binop exp | unop exp

x   prefixexp ::= var | functioncall | `(´ exp `)´

x   functioncall ::=  prefixexp args | prefixexp `:´ Name args

x   args ::=  `(´ [explist] `)´ | tableconstructor | String

    function ::= function funcbody

    funcbody ::= `(´ [parlist] `)´ block end

    parlist ::= namelist [`,´ `...´] | `...´

x   tableconstructor ::= `{´ [fieldlist] `}´

x   fieldlist ::= field {fieldsep field} [fieldsep]

x   field ::= `[´ exp `]´ `=´ exp | Name `=´ exp | exp

x   fieldsep ::= `,´ | `;´

x   binop ::= `+´ | `-´ | `*´ | `/´ | `^´ | `%´ | `..´ |
         `<´ | `<=´ | `>´ | `>=´ | `==´ | `~=´ |
         and | or

x   unop ::= `-´ | not | `#´

Source: <http://www.lua.org/manual/5.1/manual.html#8>
